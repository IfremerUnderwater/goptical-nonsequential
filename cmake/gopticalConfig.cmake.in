# Config file for Goptical library - Find Goptical & dependencies.
#
# This file is used by CMake when FIND_PACKAGE( Goptical ) is invoked (and
# the directory containing this file is present in CMAKE_MODULE_PATH).
#
# This module defines the following variables:
#
# GOPTICAL_FOUND: True if Goptical has been successfully found.
#
# GOPTICAL_VERSION: Version of Goptical found.
#

# Called if we failed to find Goptical or any of it's required dependencies,
# unsets all public (designed to be used externally) variables and reports
# error message at priority depending upon [REQUIRED/QUIET/<NONE>] argument.
macro(GOPTICAL_REPORT_NOT_FOUND REASON_MSG)
  # FindPackage() only references Goptical_FOUND, and requires it to be
  # explicitly set FALSE to denote not found (not merely undefined).
  set(GOPTICAL_FOUND FALSE)

  # Reset the CMake module path to its state when this script was called.
  set(CMAKE_MODULE_PATH ${CALLERS_CMAKE_MODULE_PATH})

  # Note <package>_FIND_[REQUIRED/QUIETLY] variables defined by
  # FindPackage() use the camelcase library name, not uppercase.
  if (GOPTICAL_FIND_QUIETLY)
    message(STATUS "Failed to find GOPTICAL - " ${REASON_MSG} ${ARGN})
  elseif (GOPTICAL_FIND_REQUIRED)
    message(FATAL_ERROR "Failed to find GOPTICAL - " ${REASON_MSG} ${ARGN})
  else()
    # Neither QUIETLY nor REQUIRED, use SEND_ERROR which emits an error
    # that prevents generation, but continues configuration.
    message(SEND_ERROR "Failed to find GOPTICAL - " ${REASON_MSG} ${ARGN})
  endif ()
  return()
endmacro(GOPTICAL_REPORT_NOT_FOUND)

# set the version.
set(GOPTICAL_VERSION @GOPTICAL_VERSION@)

# Get the (current, i.e. installed) directory containing this file.
get_filename_component(CURRENT_CONFIG_INSTALL_DIR
  "${CMAKE_CURRENT_LIST_FILE}" PATH)

# Record the state of the CMake module path when this script was
# called so that we can ensure that we leave it in the same state on
# exit as it was on entry, but modify it locally.
set(CALLERS_CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH})
# Reset CMake module path to the installation directory of this
# script.
set(CMAKE_MODULE_PATH ${CURRENT_CONFIG_INSTALL_DIR})

# Build the absolute root install directory as a relative path
get_filename_component(CURRENT_ROOT_INSTALL_DIR
  ${CMAKE_MODULE_PATH}/../../../ ABSOLUTE)
if (NOT EXISTS ${CURRENT_ROOT_INSTALL_DIR})
  GOPTICAL_REPORT_NOT_FOUND(
    "Goptical install root: ${CURRENT_ROOT_INSTALL_DIR}, "
    "determined from relative path from GopticalConfig.cmake install location: "
    "${CMAKE_MODULE_PATH}, does not exist.")
endif (NOT EXISTS ${CURRENT_ROOT_INSTALL_DIR})

# Check if Goptical header is installed
if (NOT EXISTS ${CURRENT_ROOT_INSTALL_DIR}/include/goptical/core/common.hpp)
  GOPTICAL_REPORT_NOT_FOUND(
    "Goptical install root: ${CMAKE_MODULE_PATH}. "
    "Cannot find Goptical include files.")
endif (NOT EXISTS ${CURRENT_ROOT_INSTALL_DIR}/include/goptical/core/common.hpp)

##### the libraries themselves come in via GopticalTargets-<release/debug>.cmake
# as link libraries rules as target.

set(THREADS_PREFER_PTHREAD_FLAG ON)
find_package(Threads REQUIRED)

# Import exported Goptical targets
include(${CURRENT_CONFIG_INSTALL_DIR}/GopticalTargets.cmake)

# As we use GOPTICAL_REPORT_NOT_FOUND() to abort, if we reach this point we have
# found Goptical and all required dependencies.
message(STATUS "----")
message(STATUS "Goptical Find_Package")
message(STATUS "----")
message(STATUS "Found Goptical version: ${GOPTICAL_VERSION}")
message(STATUS "Installed in: ${CURRENT_ROOT_INSTALL_DIR}")
message(STATUS "----")

set(GOPTICAL_FOUND TRUE)

# Reset the CMake module path to its state when this script was called.
set(CMAKE_MODULE_PATH ${CALLERS_CMAKE_MODULE_PATH})
